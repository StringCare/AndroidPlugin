
buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.3'
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.1"
        classpath 'org.gradle.api.plugins:gradle-nexus-plugin:0.5.1'
    }
}

plugins {
    id 'edu.wpi.first.GradleJni' version '0.3.1'
}

def siteUrl = 'https://github.com/StringCare/GradlePlugin'
def gitUrl = 'https://github.com/StringCare/GradlePlugin.git'

apply plugin: 'groovy'
apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'nexus'
apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'c'


group 'com.stringcare'
version '0.9'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.5.4'
    compile gradleApi()
    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
}

model {
    platforms {
        x86 {
            architecture "x86"
        }
        x64 {
            architecture "x86_64"
        }
    }
    components {
        signKey(JniNativeLibrarySpec) { // Use JniNativeLibrarySpec to get a JNI library
            targetPlatform "x86"
            targetPlatform "x64"
            sources {
                c {
                    source.srcDirs = ['src/main/signKey/c']
                }
            }
        }
    }
    toolChains {
        visualCpp(VisualCpp) {
            installDir 'C:\\Program Files (x86)\\Microsoft Visual Studio\\2017\\BuildTools'
        }
        clang(Clang)
    }
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                name 'StringCareAndroidPlugin'
                url siteUrl
                // Set your license
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'efraespada'
                        name 'efraespada'
                        email 'efraespada@gmail.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintrayUser")
    key = properties.getProperty("bintrayApiKey")
    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "StringCareAndroidPlugin"
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}

processResources {
    from 'src/main/groovy/jni'
    include '*.dylib', '*.dll'
}
